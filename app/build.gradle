apply plugin: 'com.android.application'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId 'org.traccar.client'
        buildConfigField 'boolean', 'HIDDEN_APP', 'false'
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 64
        versionName '6.3'
    }

    lintOptions {
        checkReleaseBuilds false
    }

    flavorDimensions 'default'
    productFlavors {
        regular {
            isDefault = true
        }
        google
        hidden {
            applicationId 'org.traccar.client.hidden'
            buildConfigField 'boolean', 'HIDDEN_APP', 'true'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.3.0-alpha01'
    implementation 'androidx.preference:preference:1.1.1'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.robolectric:robolectric:4.1'
    googleImplementation 'com.google.firebase:firebase-core:17.4.3'
    googleImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    googleImplementation 'com.google.firebase:firebase-analytics:17.4.3'
    googleImplementation 'com.google.android.gms:play-services-location:17.0.0'
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains('Google')) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'

    task copyJson(type: Copy) {
        from '../../environment/google-services'
        into '.'
        include 'traccar-client-android.json'
        rename('traccar-client-android.json', 'google-services.json')
    }

    afterEvaluate {
        preBuild.dependsOn copyJson
    }
}
